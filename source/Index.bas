B4J=true
Group=Handlers
ModulesStructureVersion=1
Type=Class
Version=10.2
@EndOfDesignText@
'Handler class
Sub Class_Globals
	Private Response As ServletResponse
	Private Method As String
End Sub

Public Sub Initialize

End Sub

Sub Handle (req As ServletRequest, resp As ServletResponse)
	Response = resp
	Method = req.Method.ToUpperCase
	Dim path As String = req.RequestURI
	If path = "/" Then
		If Method = "GET" Then
			ShowIndexPage
			Return
		End If
	Else If path = "/modal" Then
		If Method = "POST" Then
			Response.Write(GenerateModal)
			Return
		End If
		Response.SendError(405, "<h1>405 Method Not Allowed</h1>")
		Return
	End If
	Response.SendError(404, "<h1>404 Page Not Found</h1>")
End Sub

Private Sub ShowIndexPage
	Dim doc As MiniHtml
	doc.Initialize("")
	doc.Write("<!DOCTYPE html>")
	doc.Write(GenerateIndex)
	Response.Write(doc.ToString)
End Sub

#Region MiniHTML Helpers
Sub Button As MiniHtml
	Return CreateTag("button")
End Sub

Sub Div As MiniHtml
	Return CreateTag("div")
End Sub

Sub Meta As MiniHtml
	Return CreateTag("meta")
End Sub

Sub Link As MiniHtml
	Return CreateTag("link")
End Sub

Sub Script As MiniHtml
	Return CreateTag("script")
End Sub

Sub CreateTag (Name As String) As MiniHtml
	Dim tag1 As MiniHtml
	tag1.Initialize(Name)
	Return tag1
End Sub
#End Region

Sub GenerateIndex As String
	Dim html1 As MiniHtml = CreateTag("html").attr("lang", "en")
	html1.comment2(" Generated by MiniHTML2 ", True)
	Dim head1 As MiniHtml = CreateTag("head").up(html1)
	Meta.up(head1).attr("charset", "UTF-8")
	Meta.up(head1).attr("name", "viewport").attr("content", "width=device-width, initial-scale=1")
	CreateTag("title").up(head1).text("EndsMeet Server")
	Link.up(head1).attr("rel", "stylesheet").attr("href", "https://cdn.jsdelivr.net/npm/bootstrap@5.3.8/dist/css/bootstrap.min.css")
	Script.up(head1).attr("src", "https://cdn.jsdelivr.net/npm/bootstrap@5.3.8/dist/js/bootstrap.bundle.min.js")
	Script.up(head1).attr("src", "https://cdn.jsdelivr.net/npm/htmx.org@2.0.8/dist/htmx.min.js")
	
	Dim body1 As MiniHtml = CreateTag("body").up(html1)
	
	Dim div1 As MiniHtml = Div.up(body1)
	div1.cls("d-flex align-items-center")
	div1.sty("height: 100vh")
	
	Dim div2 As MiniHtml = Div.up(div1)
	div2.sty("margin: auto")

	Dim div3 As MiniHtml = Div.up(div2)
	div3.attr("id", "modals-here")
	div3.cls("modal modal-blur fade")
	div3.sty("display: none")
	div3.attr("aria-hidden", "false")
	div3.attr("tabindex", "-1")
	div3.FormatAttributes = True
	
	Dim div4 As MiniHtml = Div.up(div3)
	div4.cls("modal-dialog modal-lg modal-dialog-centered")
	div4.attr("role", "document")
	Div.up(div4).cls("modal-content").uniline

	Dim button1 As MiniHtml = Button.up(div2)
	button1.attr("hx-post", "/modal")
	button1.attr("hx-target", "#modals-here")
	button1.attr("hx-trigger", "click")
	button1.attr("data-bs-toggle", "modal")
	button1.attr("data-bs-target", "#modals-here")
	button1.cls("btn btn-primary")
	button1.text("Click me")
	button1.FormatAttributes = True
	button1.multiline
	
	Return html1.Build
End Sub

Sub GenerateModal As String
	Dim div1 As MiniHtml = Div
	div1.cls("modal-dialog modal-lg modal-dialog-centered")
	
	Dim div2 As MiniHtml = Div.up(div1)
	div2.cls("modal-content")
	
	Dim div3 As MiniHtml = Div.up(div2)
	div3.cls("modal-header")
	
	Dim h5 As MiniHtml = CreateTag("h5").up(div3)
	h5.cls("modal-title")
	h5.text("Hello EndsMeet")
	
	Dim div4 As MiniHtml = Div.up(div2)
	div4.cls("modal-body")
	
	Main.count = Main.count + 1
	Dim p1 As MiniHtml = CreateTag("p").up(div4)
	p1.text($"You clicked the button ${Main.count} time(s)!"$)
	p1.uniline
	
	Dim div5 As MiniHtml = Div.up(div2)
	div5.cls("modal-footer")
	
	Dim button1 As MiniHtml = Button.up(div5)
	button1.attr("type", "button")
	button1.cls("btn btn-secondary")
	button1.attr("data-bs-dismiss", "modal")
	button1.text("Close")
	
	Return div1.build
End Sub